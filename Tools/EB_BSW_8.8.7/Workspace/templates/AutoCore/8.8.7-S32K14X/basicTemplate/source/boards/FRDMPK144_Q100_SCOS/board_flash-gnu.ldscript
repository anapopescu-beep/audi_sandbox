/* board_flash-gnu.ldscript
 *
 * Linker script memory definitions for GNU binutils.
 *
 * (c) Elektrobit Automotive GmbH
 *
 * $Id: board_flash-gnu.ldscript 1.1 2023/04/18 07:06:27CEST Dan Dustinta (dan.dustinta) in_work  $
*/

/*	Memory types and sizes for S32K144 (active configuration):
 *	SRAM_L			= 0x8000;	32	KB	(Low-order, executable)
 *	SRAM_U			= 0x7000;	28	KB	(High-order, for data)
 *	FlexRAM			= 0x1000;	4	KB
 *	FLSH_PROG		= 0x80000;	512	KB
 *	FLSH_FlexNVM	= 0x10000;	64	KB
*/

/*	Memory types and sizes for S32K146 (here as reference):
 *	SRAM_L			= 0x10000;	64	 KB	(Low-order, executable)
 *	SRAM_U			= 0xF000;	60	 KB	(High-order, for data)
 *	FlexRAM			= 0x1000;	4	 KB
 *	FLSH_PROG		= 0x100000;	1024 KB
 *	FLSH_FlexNVM	= 0x10000;	64	 KB
*/


/*	S32K144 memory is split into 2 regions:
 *	0x00000000 - 0x1FFFFFFF	:	Low-order addresses (access through the code bus)
 *	0x20000000 - 0xFFFFFFFF	:	High-order addresses (access through the system bus)
 *	All code should be placed to the low-order addresses
 *
 *	SRAM_L	lies in low-order address space (SRAM_L must end at 0x1FFFFFFF)
 *	SRAM_U	lies in high-order address space (SRAM_U must start at 0x20000000)
 *	SRAM_L and SRAM_U are contiguous!
*/

/* This linker-script describes S32K144 memory map, please update for other S32K14x derivatives */
MEMORY
{
	reset		(RL)  : ORIGIN = 0x00000000, LENGTH = 0x100		/* reset-vector					*/
	/*
	 * Gap: addresses at 0x00000400 are reserved and not writable!
	*/
	exctable_C0	(RX)  : ORIGIN = 0x00000800, LENGTH = 0x3800	/* exception (+interrupt) table */
	rom			(RX)  : ORIGIN = 0x00004000, LENGTH = 0x7C000	/* the rest of FLASH memory		*/
	ram			(RW)  : ORIGIN = 0x20000000, LENGTH = 0x7000	/* High-order RAM (system-bus)	*/
}

/* Definition of the 'reset_vector' section */
SECTIONS
{
	/*
	 * Output section reset (Type RESET)
	*/
	reset_vector :
	{
		. = ALIGN(256);
		"board_reset_vector.o*"(.rodata .rodata.*)
		"*/board_reset_vector.o"(.rodata .rodata.*)
		"*\\board_reset_vector.o"(.rodata .rodata.*)
	} >reset
}

/* Symbols for Memory Protection: Global RAM region */
__GLBL_RAM_START	= 0x20000000;
__GLBL_RAM_END		= 0x20006FFF;

/* Symbols for Memory Protection: Global ROM region */
__GLBL_ROM_START	= 0x00000000;
__GLBL_ROM_END		= 0x0007FFFF;

ENTRY(boardResetStart);
